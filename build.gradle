plugins {
    id "fabric-loom" version "1.3-SNAPSHOT"
    id "io.github.juuxel.loom-quiltflower" version "1.+" // Quiltflower, a better decompiler
    id "io.github.p03w.machete" version "1.1.4" // automatic jar compressing on build
    id "maven-publish"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${mod_version}-${create_version}-mi"
group = project.maven_group

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://mvn.devos.one/snapshots/" }// Create, Porting Lib, Forge Tags, Milk Lib, Registrate
    maven { url = "https://maven.quiltmc.org/repository/release" }
    maven { url = "https://hephaestus.dev/release" }
    maven { url = "https://api.modrinth.com/maven" }// LazyDFU
    maven { url = "https://cursemaven.com" }// Forge Config API Port, Farmer's Delight
    maven { url = "https://maven.parchmentmc.org" }// Parchment mappings
    maven { url = "https://maven.fabricmc.net/" }
    maven { url = "https://maven.tterrag.com/" }// Flywheel
    maven { url = "https://storage.googleapis.com/devan-maven/" }
    maven { url = "https://maven.shedaniel.me/" }// Cloth Config, REI
    maven { url = "https://aperlambda.github.io/maven" }
    maven { url = "https://ladysnake.jfrog.io/artifactory/mods" }
    maven { url = "https://maven.kotlindiscord.com/repository/terraformers/" }
    maven { url = "https://maven.terraformersmc.com/" }// Mod Menu
    maven { url = "https://maven.cafeteria.dev/releases/" }
    maven { url = "https://jitpack.io/" }// Mixin Extras, Fabric ASM
    maven { url = "https://maven.gegy.dev" }
    maven { url = "https://dvs1.progwml6.com/files/maven/" }// JEI
    maven { url = "https://modmaven.dev" }
    maven { url = "https://maven.gudenau.net" }
    maven { url = "https://maven.blamejared.com/vazkii/botania/Botania/" }
    maven { url = "https://maven.kyrptonaught.dev/" }
    maven { url = "https://maven.jamieswhiteshirt.com/libs-release" }// Reach Entity Attributes
    maven { url = "https://maven.quiltmc.org/repository/release" } // Quilt Mappings

}

dependencies {
    // Setup
    minecraft("com.mojang:minecraft:${minecraft_version}")
    mappings("net.fabricmc:yarn:${YARN_MAPPINGS}:v2")
    modImplementation("net.fabricmc:fabric-loader:${fabric_loader_version}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${fabric_api_version}")


    modImplementation("com.simibubi.create:create-fabric-${minecraft_version}:${create_version}")

    modImplementation("curse.maven:farmersdelightfabric-482834:${farmersdelight_version}")

    modCompileOnly("me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}")

    modCompileOnly("dev.emi:emi:${emi_version}:api")

}

processResources {
    // require dependencies to be the version compiled against or newer
    Map<String, String> properties = new HashMap<>()
    properties.put("version", version)
    properties.put("fabric_loader_version", fabric_loader_version)
    properties.put("fabric_api_version", fabric_api_version)
    properties.put("create_version", create_version)
    properties.put("minecraft_version", minecraft_version)

    properties.forEach((k, v) -> inputs.property(k, v))

    filesMatching("fabric.mod.json") {
        expand properties
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = Integer.parseInt(sourceCompatibility)
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
    exclude("com/simibubi/create/content/contraptions/components/deployer/DeployerBlockEntity.class", "com/simibubi/create/content/contraptions/components/deployer/DeployerBlockEntity\$State.class", "com/simibubi/create/content/contraptions/components/deployer/DeployerBlockEntity\$Mode.class", "com/simibubi/create/content/contraptions/components/deployer/DeployerBlockEntity\$1.class")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        mavenLocal()
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
loom {
    accessWidenerPath = file("src/main/resources/${mod_id}.accesswidener")
    runs {
        // This adds a new gradle task that runs the datagen API: "gradlew runDatagenClient"
        datagenClient {
            inherit client
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=${mod_id}"

            runDir "build/datagen"
        }
    }
}

// Adds the generated files into the jar you distribute to players.
sourceSets {
    main {
        resources {
            srcDirs += ['src/main/generated']
        }
    }
}